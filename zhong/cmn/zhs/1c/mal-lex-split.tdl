mal_clause_和_c := conj_-_e_le &
  """
  using 和 to coordinate clauses.
  FIXME: this should probably be introducing the relation 也
  FIXME: This is not working the the example below because of the comma. I'm not sure we .
  <mex>他 不 学 中文 ， 和 不 学 法文 。
  <mex>他 学 中文 ， 和 学 法文 。
  """
  [ STEM < "和" >,
    SYNSEM [ LOCAL.CAT.HEAD.CHAR [ FCHAR "和",
                                   LENGTH one ],
             LKEYS.KEYREL.PRED "_和_c_⚠_rel" ],
    TRAITS native_token_list ].

mal_scopal_也_r := adv_-_scop_le &
  """
  A mal-lexical entry for 也, that allows it to be placed before the suject (i.e. as sentencial adverb)
  FIXME: DMRS cannot be created
  FIXME: it competes with 也_r_1 in the correct position; it would be best to restrict it further;
  FIXME: this should probably be introducing the relation 也
  <mex> 我 学 工程 ， 也 我 学 中文 。
  """
  [ STEM < "也" >,
    SYNSEM [ LOCAL.CAT.HEAD.CHAR [ FCHAR "也",
                                   LENGTH one ],
             LKEYS.KEYREL.PRED "_也_a_⚠_rel" ],
    TRAITS native_token_list ].

mal_extra_吗_qp := mal_x_-_qpart_le &
  """
  This mal 吗 is attaching to sentences that are already a question.
  <mex>  你 学习 什么 吗 ？
  """
  [ STEM < "吗" >,
    SYNSEM.LOCAL.CAT.HEAD.CHAR [ FCHAR "吗",
                                 LENGTH one ],
    TRAITS native_token_list ].

mal_apred_是_cop := mal_v_adj_cop_le &
  """
  This 是 is a special copula that predicates over adjectival phrases. This is generally considered incorrect, unless as part of a focus 是---的 construction, or in a contrastive situation;
  <mex>他 是 漂亮 。
  <mex>蛋糕 是 好吃 。
  """
  [ STEM < "是" >,
    SYNSEM.LOCAL.CAT.HEAD.CHAR [ FCHAR "是",
                                 LENGTH one ],
    TRAITS native_token_list ].

