;;; -*- Mode: TDL; Coding: utf-8 -*-
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Grammar of Cantonese
;;; created at:
;;;     Fri Aug 25 04:10:28 UTC 2014
;;; created by Sanghoun Song (SSH, 2014-08-25)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; SSH 2015-04-28: FIXME
;; The followings are Mandarin-speicific
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;; JUS/SSH 2015-04-30: Cantonese-specific aspect
;; (http://en.wikipedia.org/wiki/Cantonese_grammar#Verbal_aspect)
progressive := durative.
continuous := durative.
habitual := aspect. 


;; JUS/SSH 2015-04-30: FIXME 
+vjp :+ [ LZG lzg ].
lzg := avm. 
le := lzg.
zhe := lzg.
guo := lzg.
no-lzg := lzg.
le+zhe := le & zhe.
le+guo := le & guo.
zhe+guo := zhe & guo.
le+zhe+guo := le & zhe & guo.

basic-pred-adj-lex :+ [ SYNSEM.LOCAL.CAT.HEAD.LZG no-lzg ]. 

pfv-marker := aspect-markers & 
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ BOUND na-or--,
				    LOCAL.CONT.HOOK.INDEX.E.ASPECT perfective ] > ] ].

exp-marker := aspect-markers & 
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ LOCAL.CONT.HOOK.INDEX.E.ASPECT experiential ] > ] ].
  
dur-marker := aspect-markers & 
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ BOUND na-or--,
				    LOCAL.CONT.HOOK.INDEX.E.ASPECT durative ] > ] ].

prog-marker := aspect-markers & 
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ BOUND na-or--,
				    LOCAL.CONT.HOOK.INDEX.E.ASPECT progressive ] > ] ].

cont-marker := aspect-markers & 
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ BOUND na-or--,
				    LOCAL.CONT.HOOK.INDEX.E.ASPECT continuous ] > ] ].

delim-marker := aspect-markers & 
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ BOUND na-or--,
				    LOCAL.CONT.HOOK.INDEX.E.ASPECT delimitative ] > ] ].

habit-marker := aspect-markers & 
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ BOUND na-or--,
				    LOCAL.CONT.HOOK.INDEX.E.ASPECT habitual ] > ] ].


crs-lex-item := spart-lex-item &
  [ STYLE robust,
    SYNSEM [ R-PERIPH +,
	     SPART crs,
	     LOCAL.CAT [ VAL [ COMPS < [ SPART not-crs-not-ma,
					 LOCAL.CAT [ HEAD +vj,
						     VAL [ SUBJ < >,
							   COMPS < > ],
						     MC #mc ],
					 LOCAL.CONT.HOOK.INDEX.SF prop,
					 NON-LOCAL.SLASH 0-dlist ] >,
			       SPEC < >],
			 MC #mc ] ] ].

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;classifier-lex :+  
;  [ SYNSEM.LOCAL.CAT.HEAD.MOD < [ LOCAL.CONT.HOOK.INDEX.COG-ST activ-or-less ] > ]. 

head-spec-phrase :+ 
 [ HEAD-DTR.SYNSEM.LOCAL.CAT.HEAD.CLASSIFIED na-or-+ ].

bare-np-phrase :+ [ HEAD-DTR.SYNSEM.BARE na-or-+ ].

count-noun-lex :+ no-classifier-rule-dtr.

;; SSH 2015-04-16 [NONSUBJ +] or not?: how to constrain generic readings?
no-classifier-lex-rule := const-add-only-lex-rule & 
 [ SYNSEM.BARE +,
   SYNSEM.LOCAL [ CAT [ NONSUBJ +,
			HEAD [ GENERIC -,
			       CLASSIFIED - ] ],
		  CONT.HOOK.INDEX.COG-ST type-id ],
   DTR no-classifier-rule-dtr,
   C-CONT [ RELS <! !>, HCONS <! !>, ICONS <! !> ] ].

no-classifier-rule-dtr := word-or-lexrule.

generic-np-phrase :+ 
 [ HEAD-DTR.SYNSEM lex-synsem ].



;; SSH 2015-05-07: Cantonese-specific predsort.
_唔_x_rel := neg_x_rel.
_冇_x_rel := neg_x_rel.
_冇_polar_x_rel := neg_x_rel.

_嘅_demons_rel := demons_rel.
_嘅_nom_rel := nm_rel.

;; SSH 2015-05-07: Cantonese classifiers

_個_x_rel := cl_x_rel.
_枝_x_rel := cl_x_rel.
_隻_x_rel := cl_x_rel.
_匹_x_rel := cl_x_rel.
_件_x_rel := cl_x_rel.
_條_x_rel := cl_x_rel.
_台_x_rel := cl_x_rel.
